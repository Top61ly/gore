cmake_minimum_required(VERSION 3.20)

project(SampleApp)

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

if(WIN32)
    set(TARGET_PLATFORM "Windows")
elseif(UNIX AND NOT APPLE)
    set(TARGET_PLATFORM "Linux")
elseif(APPLE)
    set(TARGET_PLATFORM "macOS")
endif()

if (${TARGET_PLATFORM} STREQUAL "Windows")
    set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin)
    set(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin)
    set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/lib)
elseif(${TARGET_PLATFORM} STREQUAL "Linux")
    set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin)
    set(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/lib)
    set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/lib)
    set(CMAKE_BUILD_WITH_INSTALL_RPATH TRUE)
    set(CMAKE_INSTALL_RPATH "$ORIGIN/../lib")
elseif(${TARGET_PLATFORM} STREQUAL "macOS")
    set(CMAKE_MACOSX_BUNDLE TRUE)
    set(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}.app/Contents/Frameworks)
    set(CMAKE_BUILD_WITH_INSTALL_RPATH TRUE)
    set(CMAKE_INSTALL_RPATH "@executable_path/../Frameworks")
endif()

add_subdirectory(../src gore)

set(COMMON_SOURCE_FOLDERS "App" "Scripts" "Scripts/Utils")
set(COMMON_INCLUDE_FOLDERS "${CMAKE_CURRENT_SOURCE_DIR}")
set(COMMON_DEFINITIONS "ENGINE_DLL")
set(COMMON_LIBRARIES gore)

if(${TARGET_PLATFORM} STREQUAL "macOS")
    find_library(FOUNDATION Foundation)

    set(PLATFORM_LIBRARIES ${FOUNDATION})
else()
    set(PLATFORM_LIBRARIES)
endif()


set(COMMON_SOURCES "main.cpp")
foreach(folder ${COMMON_SOURCE_FOLDERS})
    file(GLOB_RECURSE SOURCES "${folder}/*.cpp" "${folder}/*.h")
    list(APPEND COMMON_SOURCES ${SOURCES})
endforeach()


add_executable(${PROJECT_NAME} ${COMMON_SOURCES})

target_include_directories(${PROJECT_NAME} PRIVATE ${COMMON_INCLUDE_FOLDERS})
target_compile_definitions(${PROJECT_NAME} PRIVATE ${COMMON_DEFINITIONS})
target_link_libraries(${PROJECT_NAME} PRIVATE ${COMMON_LIBRARIES} ${PLATFORM_LIBRARIES})

if(${TARGET_PLATFORM} STREQUAL "macOS")
    set_target_properties(${PROJECT_NAME} PROPERTIES
        BUNDLE True
        MACOSX_BUNDLE_GUI_IDENTIFIER com.gore.${PROJECT_NAME}
        MACOSX_BUNDLE_BUNDLE_NAME ${PROJECT_NAME}
        MACOSX_BUNDLE_BUNDLE_VERSION "0.1"
        MACOSX_BUNDLE_SHORT_VERSION_STRING "0.1"
    )
endif()